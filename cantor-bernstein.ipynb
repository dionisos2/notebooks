{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Introduction"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Le but de ce document est d’expliquer une démonstration du théorème de Cantor-Berntein : \"S'il existe une injection d'un ensemble $E$ vers un ensemble $F$ et une injection de $F$ vers $E$, alors il existe une bijection de $E$ sur $F$.\"\n",
    "\n",
    "Puis de créer un programme qui génère une bijection à partir des deux injections en s’inspirant de cette démonstration, et enfin de s’en servir sur un exemple. (solution non générale)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Explication avec les mains"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Nommons $f$ l’injection de $E$ sur $F$ et $g$ l’injection de $F$ sur $E$. On va essayer de construire une fonction $h$ bijective de $E$ dans $F$. \n",
    "\n",
    "Pour commencer on va utiliser $g^{-1}$ qui est bien surjective (car $g$ est défini sur tout $F$) et injective car c’est une fonction inverse.  \n",
    "Le problème est que cette fonction n’est pas défini sur tout $E$ (car $g$ n’était pas surjective). On utilisera alors $f$ pour l’ensemble manquant pour couvrir tout $E$, qu’on nommera $C_0=E \\backslash g(F)$. \n",
    "\n",
    "On a donc $\n",
    "h(x) =\n",
    "\\begin{cases}\n",
    "      f(x) & si,x\\in C_0 \\\\\n",
    "      g^{-1}(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$\n",
    "\n",
    "$h$ est bien définit sur tout $E$ et surjective par construction, le problème qu’on a maintenant est que $f$ va rentrer en collision avec $g^{-1}$ sur $F$ car $g^{-1}$ occupait déjà tout $F$.  \n",
    "Pour régler ce problème on va prendre toutes les valeurs $y$ de l’ensemble $F$ où il y a une collision (qui est en réalité tout $f(C_0)$, puis regarder l’antécédent de $y$ par la fonction $g^{-1}$ (qui est donc $g(y)$), et ne plus utiliser $g^{-1}$ pour ce point là. (car il est déjà couvert par $f$).\n",
    "\n",
    "On obtient ainsi un nouvel ensemble $C_1=g(f(C_0))$\n",
    "\n",
    "![schéma](images/schema1.png)\n",
    "\n",
    "Notre fonction $h$ devient donc :\n",
    "\n",
    "$h(x) =\n",
    "\\begin{cases}\n",
    "      f(x) & si,x\\in C_0 \\cup C_1 \\\\\n",
    "      g^{-1}(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$\n",
    "\n",
    "On a réglé le problème de collision pour $C_0$, malheureusement on se retrouve avec exactement le même problème pour $C_1$. Ouais je sais nous voilà bien avancé, mais pas de panique (si si vous paniquiez je l’ai senti). \n",
    "\n",
    "On ne se laisse par abattre et on répète cette opération une infinité de fois ($Cn+1=g(f(Cn))$). Et vue que quand même l’infini c’est beaucoup, ça va bien finir par marcher !\n",
    "\n",
    "Plus sérieusement ^^, si maintenant je prend un point $x$ quelconque sur $F$ il y a deux possibilités :\n",
    "- Soit il n’y a jamais eu de collisions, et il n’y a donc pas de problèmes.\n",
    "- Soit il y a eu une collision à une des étapes, or cette collision a donc été corrigée à l’étape suivante, et il n’y a donc plus de problèmes à partir de là. (on ne peut pas se retrouver deux fois avec la même collision)\n",
    "\n",
    "Donc $h$ est bijective !\n",
    "\n",
    "$h(x) =\n",
    "\\begin{cases}\n",
    "      f(x) & si,x\\in \\bigcup _{i=0}^\\infty C_n \\\\\n",
    "      g^{-1}(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$\n",
    "\n",
    "Maintenant, place à la démonstration."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Démonstration"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "$f$ l’injection de $E$ sur $F$ et $g$ l’injection de $F$ sur $E$. On va essayer de construire une fonction $h$ bijective de $E$ dans $F$.\n",
    "\n",
    "Non reprend notre contruction pour $C$ et pour $h$ :  \n",
    "$C_0=E \\backslash g(F)$  \n",
    "$C_n+1 = g(f(C_n))$  \n",
    "$C = \\bigcup _{i=0}^\\infty C_n$\n",
    "\n",
    "$h(x) =\n",
    "\\begin{cases}\n",
    "      f(x) & si x \\in C \\\\\n",
    "      g^{-1}(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$\n",
    "\n",
    "Puis on démondre l’injection :  \n",
    "$\n",
    "\\begin{align}\n",
    "&(x, y) \\in C^2, &h(x) = h(y)& & \\Rightarrow &f(x)=f(y) \\Rightarrow x=y\\\\\n",
    "&x \\notin C \\land y \\notin C, &h(x) = h(y)& & \\Rightarrow &g^{-1}(x)=g^{-1}(y) \\Rightarrow x=y\\\\\n",
    "&x \\notin C \\land y \\in C, &h(x)=h(y)& & \\Rightarrow &g^{-1}(x)=f(y) \\Rightarrow x=g(f(y)) \\Rightarrow x \\in C \\Rightarrow \\text{contradiction et mensonge}\n",
    "\\end{align}\n",
    "$ \n",
    "\n",
    "Et enfin la surjection :  \n",
    "\n",
    "$\n",
    "\\begin{align*}\n",
    "& y \\in f(C) && \\Rightarrow \\exists x \\in C, y=f(x)=h(x)\\\\\n",
    "\\\\\n",
    "& y \\notin f(C) \\land g(y) \\notin C && \\Rightarrow h(g(y)) = g^{-1}(g(y)) = y\\\\\n",
    "& y \\notin f(C) \\land g(y) \\in C_0 && \\Rightarrow g(y) \\in (E \\backslash g(F)) \\Rightarrow faux\\\\\n",
    "\\\\\n",
    "& y \\notin f(C) \\land g(y) \\in C_{n+1} && \\Rightarrow g(y) \\in g(f(C_n)) \\\\\n",
    "& && \\Rightarrow \\exists x \\in C, g(y)=g(f(x))=g(h(x)) \\\\\n",
    "& && \\Rightarrow \\exists x \\in C, y=g^{-1}(g(h(x)))=h(x) \\\\\n",
    "\\end{align*}\n",
    "$\n",
    "\n",
    "On a bien testé tous les cas possibles :  \n",
    "$\\forall y \\in F, y \\in f(C) \\lor y \\notin f(C) \\land (g(y) \\notin C \\lor g(y) \\in C_0 \\lor g(y) \\in C_{n>0})$  \n",
    "donc :  \n",
    "$\\forall y \\in F, \\exists x \\in E, y=h(x)$\n",
    "\n",
    "$h$ est injective et surjective, donc $h$ est bijective."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Explication sur le programme"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "On va créer une fonction \"create_h\", qui à partir des injections $f$ et $g$, et de leurs inverses $f^{-1}$, $g^{-1}$, génère la fonction bijective $h$ et son inverse $h^{-1}$.\n",
    "\n",
    "Par convention les fonctions produiront une erreur \"DomainError\" quand elles sont utilisées en dehors de leur domaine.\n",
    "Ainsi par exemple, pour savoir si $x \\in D_{g^{-1}}$ il suffirat de vérifier si $g^{-1}(x)$ produit cette erreur, et de la capturer.\n",
    "\n",
    "La principale complexité pour construire $h$, est de vérifier si $x \\in C$.  \n",
    "Celà revient à vérifier $x \\in C_0 \\lor x \\in C_{n>0}$  \n",
    "Vérifier $x \\in C_0 \\Leftrightarrow x \\in E \\backslash g(F)$ autrement dit celà revient à vérifier $x \\notin g(F)$  \n",
    "Or $x \\in g(F) \\Leftrightarrow \\exists y, x=g(y) \\Leftrightarrow x \\in D_{g^{-1}} (y = g^{-1}(x))$  \n",
    "On peut donc facilement vérifier la première partie \"$x \\in C_0$\", en vérifiant \"$x \\notin D_{g^{-1}}$\"\n",
    "\n",
    "$\n",
    "\\begin{align*}\n",
    "& x \\in C_{n>0} &&\\Leftrightarrow x \\in g(f(C_{n-1}))\\\\\n",
    "& && \\Leftrightarrow \\exists z \\in C_{n-1}, x=g(f(z)) \\quad \\text{cad : } z=f^{-1}(g^{-1}(x))\\\\\n",
    "& && \\Leftrightarrow (x \\in D_{g^{-1}}) \\land (g^{-1}(x) \\in D_{f^{-1}})  \\land (f^{-1}(g^{-1}(x)) \\in C_{n-1})\n",
    "\\end{align*}\n",
    "$\n",
    "\n",
    "Cela nous donne une fonction recursive pour vérifier $x \\in C_n$, et sans avoir besoin de connaitre la valeur de $n$ à chaque étape, on est assuré que si $x \\in C_n$, alors $f^{-1}(g^{-1}(x)) \\in C_{n-1}$ On vérifiera donc l’appartenance à $C_n$ pour des valeurs de plus en plus petite de $n$, jusqu’à attendre $C_0$ qui terminera la récursion.  \n",
    "Par contre je ne suis pas sur que si $x \\notin C_n$ cette fonction se termine obligatoirement (cependant c’est le cas pour l’exemple de l’article).\n",
    "On peut donc maintenant facilement créé $h$ comme définit précédement.\n",
    "\n",
    "$h(x) =\n",
    "\\begin{cases}\n",
    "      f(x) & si x \\in C \\\\\n",
    "      g^{-1}(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$\n",
    "\n",
    "De plus :  \n",
    "$y \\in f(C) \\Leftrightarrow \\exists x \\in C, y=f(x)=h(x) \\Leftrightarrow x=h^{-1}(y)=f^{-1}(y)$  \n",
    "Or si $h^{-1}(y) \\neq f^{-1}(y)$ alors $h^{-1}(y)$ ne peut être égal qu’à $g(y)$.\n",
    "\n",
    "\n",
    "Donc :  \n",
    "$h^{-1}(x) =\n",
    "\\begin{cases}\n",
    "      f^{-1}(x) & si x \\in f(C) \\\\\n",
    "      g(x) & sinon \\\\\n",
    "\\end{cases}\n",
    "$"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# La fonction \"create_h\""
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m registry at `~/.julia/registries/General`\n",
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m git-repo `https://github.com/JuliaRegistries/General.git`\n",
      "\u001b[2K\u001b[?25h[1mFetching:\u001b[22m\u001b[39m [========================================>]  100.0 %.0 % [==========>                              ]  23.4 %]  46.6 %\u001b[36m\u001b[1mFetching:\u001b[22m\u001b[39m [=========================>               ]  60.9 %>  ]  95.0 %\u001b[32m\u001b[1m Resolving\u001b[22m\u001b[39m package versions...\n",
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m `~/.julia/environments/v1.2/Project.toml`\n",
      "\u001b[90m [no changes]\u001b[39m\n",
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m `~/.julia/environments/v1.2/Manifest.toml`\n",
      "\u001b[90m [no changes]\u001b[39m\n",
      "\u001b[32m\u001b[1m Resolving\u001b[22m\u001b[39m package versions...\n",
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m `~/.julia/environments/v1.2/Project.toml`\n",
      "\u001b[90m [no changes]\u001b[39m\n",
      "\u001b[32m\u001b[1m  Updating\u001b[22m\u001b[39m `~/.julia/environments/v1.2/Manifest.toml`\n",
      "\u001b[90m [no changes]\u001b[39m\n"
     ]
    }
   ],
   "source": [
    "# Just some julia package\n",
    "using Pkg\n",
    "Pkg.add(\"Primes\")\n",
    "Pkg.add(\"SaferIntegers\");"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "create_h (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "\"Check if x is in the domain of f. (ie check than f(x) is well defined)\"\n",
    "function in_domain(f, x)\n",
    "    try\n",
    "        y = f(x)\n",
    "    catch error\n",
    "        if isa(error, DomainError)\n",
    "            return false\n",
    "        else\n",
    "            rethrow()\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    return true\n",
    "end\n",
    "\n",
    "function create_h((f, finv), (g, ginv))\n",
    "    # C0 = E - g(f)\n",
    "    in_C0(x) = !in_domain(ginv, x)\n",
    "    \n",
    "    function in_Cn(x)\n",
    "        if in_C0(x)\n",
    "            return true\n",
    "        end\n",
    "        \n",
    "        y = ginv(x)\n",
    "        if in_domain(finv, y)\n",
    "            old_x = finv(y)\n",
    "            return in_Cn(old_x)\n",
    "        else\n",
    "            return false\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    h(x) = in_Cn(x) ? f(x) : ginv(x) \n",
    "    hinv(x) = in_domain(finv, x) && in_Cn(finv(x)) ? finv(x) : g(x)\n",
    "    return (h, hinv)\n",
    "end\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Un exemple : Les séquences finies"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Considérons l'ensemble de toutes les séquences finies de nombres naturels, noté $S$  (En guise d'exemple, $(4, 0, 1, 68)$ est une séquence finie).\n",
    "\n",
    "On va essayer de trouver une bijection de $S$ dans $\\mathbb{N}$.  \n",
    "Pour cela on va chercher une injection $f$ de $S$ dans $\\mathbb{N}$, puis une injection $g$ de $\\mathbb{N}$ dans $S$, trouver leurs inverses, et enfin générer les bijections $h$ et $h^{-1}$ à l’aide de notre super fonction \"create_h\".  \n",
    "On finira en calculant $h(x)$ et $h^{-1}(y)$ pour quelques valeurs de $x$ et de $y$ en guise d’exemple.\n",
    "\n",
    "En prenant $f(X) = \\prod P_i^{X_i+1}$ avec $P_i$ le Ième nombre premier, on voit facilement que $f$ est injective car il n’existe qu’une seule décomposition d’un entier en produit de facteurs premiers.\n",
    "\n",
    "Et $g(x)=(x)$ est une injection évidente."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "h((0,)) = 2\n",
      "hinv(2) = (0,)\n",
      "h((1,)) = 1\n",
      "hinv(1) = (1,)\n",
      "h((2,)) = 8\n",
      "hinv(8) = (2,)\n",
      "h((3,)) = 3\n",
      "hinv(3) = (3,)\n",
      "h((4,)) = 4\n",
      "hinv(4) = (4,)\n",
      "h((5,)) = 5\n",
      "hinv(5) = (5,)\n",
      "h((6,)) = 128\n",
      "hinv(128) = (6,)\n",
      "h((7,)) = 7\n",
      "hinv(7) = (7,)\n",
      "h((8,)) = 512\n",
      "hinv(512) = (8,)\n",
      "h((9,)) = 9\n",
      "hinv(9) = (9,)\n",
      "h((10,)) = 10\n",
      "hinv(10) = (10,)\n",
      "h((11,)) = 11\n",
      "hinv(11) = (11,)\n",
      "h((12,)) = 8192\n",
      "hinv(8192) = (12,)\n",
      "h((13,)) = 13\n",
      "hinv(13) = (13,)\n",
      "h((14,)) = 14\n",
      "hinv(14) = (14,)\n",
      "h((15,)) = 15\n",
      "hinv(15) = (15,)\n",
      "h((16,)) = 16\n",
      "hinv(16) = (16,)\n",
      "h((17,)) = 17\n",
      "hinv(17) = (17,)\n",
      "h((18,)) = 524288\n",
      "hinv(524288) = (18,)\n",
      "h((19,)) = 19\n",
      "hinv(19) = (19,)\n",
      "h((20,)) = 20\n",
      "hinv(20) = (20,)\n",
      "--------------------------------------------------\n",
      "hinv(0) = ()\n",
      "hinv(1) = (1,)\n",
      "hinv(2) = (0,)\n",
      "hinv(3) = (3,)\n",
      "hinv(4) = (4,)\n",
      "hinv(5) = (5,)\n",
      "hinv(6) = (0, 0)\n",
      "hinv(7) = (7,)\n",
      "hinv(8) = (2,)\n",
      "hinv(9) = (9,)\n",
      "hinv(10) = (10,)\n",
      "hinv(11) = (11,)\n",
      "hinv(12) = (1, 0)\n",
      "hinv(13) = (13,)\n",
      "hinv(14) = (14,)\n",
      "hinv(15) = (15,)\n",
      "hinv(16) = (16,)\n",
      "hinv(17) = (17,)\n",
      "hinv(18) = (0, 1)\n",
      "hinv(19) = (19,)\n",
      "hinv(20) = (20,)\n"
     ]
    }
   ],
   "source": [
    "using Primes\n",
    "using SaferIntegers\n",
    "\n",
    "S = NTuple{N,Int} where N\n",
    "\"An injection from S to ℕ (where S is the set of all finite sequences)\"\n",
    "function f(x::S)::Int\n",
    "    if isempty(x)\n",
    "        return 0\n",
    "    end\n",
    "    res = SafeInt(1)\n",
    "    for (i, xi) in enumerate(x)\n",
    "        ti = SafeInt(prime(i))^(xi+1)\n",
    "        res *= ti\n",
    "    end\n",
    "    return res\n",
    "end\n",
    "\n",
    "\"An basic injection from ℕ to S\"\n",
    "g(x::Int)=(x,)\n",
    "\n",
    "ginv(x::S)=length(x) == 1 ? x[1] : throw(DomainError(\"ginv only defined for singleton\"));\n",
    "\n",
    "function finv(x::Int)\n",
    "    if x == 0\n",
    "        return ()\n",
    "    end\n",
    "    if x == 1\n",
    "        throw(DomainError(\"finv(1) undefined (∄x, f(x)==1)\"))\n",
    "    end\n",
    "    factors = factor(x)\n",
    "    res = []\n",
    "    for (i, f) in enumerate(factors)\n",
    "        if f[1] != prime(i)\n",
    "            throw(DomainError(\"finv($x) undefined (∄x, f(x)==$x)\"))\n",
    "        end\n",
    "        push!(res, f[2]-1)\n",
    "    end\n",
    "    return Tuple(res)\n",
    "end\n",
    "\n",
    "\n",
    "(h, hinv) = create_h((f, finv), (g, ginv))\n",
    "\n",
    "for x in 0:20\n",
    "    y = h((x,))\n",
    "    @eval @show h(($x,));\n",
    "    @eval @show hinv($y);\n",
    "end\n",
    "\n",
    "println(\"-\"^50)\n",
    "\n",
    "for y in 0:20\n",
    "    @eval @show hinv($y)\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.2.0",
   "language": "julia",
   "name": "julia-1.2"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.2.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
